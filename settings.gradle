//Based on tde
pluginManagement {
    repositories {
        if (use_mirror.toBoolean() || gradle.startParameter.taskNames[0] == "MirrorSetup") {
            removeIf {
                it instanceof MavenArtifactRepository &&
                    (it.url.toString() == "https://repo.maven.apache.org/maven2/")
            }
            maven {
                name 'Ali Mirror Maven'
                url "https://maven.aliyun.com/nexus/content/groups/public"
                allowInsecureProtocol = true
            }
            maven {
                name 'MC Mirror Maven'
                url "https://${mirror_maven_url}"
                allowInsecureProtocol = true
            }
            //more mirror maven add here,in order to search plugins
        }
        maven {
            // RetroFuturaGradle
            name 'GTNH Maven'
            url 'http://jenkins.usrv.eu:8081/nexus/content/groups/public/'
            allowInsecureProtocol = true
            mavenContent {
                includeGroup 'com.gtnewhorizons'
                includeGroup 'com.gtnewhorizons.retrofuturagradle'
            }
        }
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    // Automatic toolchain provisioning
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.4.0'
}

// Due to an IntelliJ bug, this has to be done
// rootProject.name = archives_base_name
rootProject.name = rootProject.projectDir.getName()


println """
Java Version: ${System.getProperty 'java.version'} ${System.getProperty 'os.arch'} VM ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'})
JAVA_HOMEï¼š${System.getenv 'JAVA_HOME'}
Java Path: ${System.getProperty 'java.home'}
"""
def javaEdition = "DK"
def javac = System.getProperty('os.name').toLowerCase(Locale.ENGLISH).contains("windows") ? "\\..\\bin\\javac.exe" : "/../bin/javac"
if (!file(System.getProperty('java.home') + javac).exists()) {
    javaEdition = "RE"
}
if (!System.getProperty('java.version').startsWith("1.8.") || javaEdition == "RE") {
    """
LISTEN: YOU ARE USEING THE [ J${javaEdition} ${System.getProperty('java.version')} ],PLEASE USE JDK 1.8 (NOT JRE 1.8),REMEMBER THAT JAVA_HOME MUST POINT TO JDK 1.8
                            !!!! SEE HOW TO CONFIGUTURE YOUR JAVA_HOME !!!!
    """.with {
        println it
        throw new RuntimeException(it)
    }
}